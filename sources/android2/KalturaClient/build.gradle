
apply plugin: 'com.android.library'

try {
    apply plugin: 'com.github.dcendents.android-maven'
} catch (UnknownPluginException ignored) {
    // That's ok
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }
}

// Build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// Build Javadoc
task javadoc(type: Javadoc) {
    println("üî¥üîµÔ∏è Note: Javadoc generation creates a lot of classpath errors, but they are harmless üîµüî¥")
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// Build a jar with Javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Includes sources and javadoc in the publication
artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
}
